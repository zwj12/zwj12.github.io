---
layout: post
title: "CRC Module"
date: 2021-03-15 20:28:00 +0800
author: Michael
categories: robot
---

# CRC参数Info

1. Name：CRC校验算法的名称（CRC-4/ITU）；

2. Width：CRC校验值的长度（此例中10011长度为5）；

3. Poly：多项式（省略了最高位的1；即10011==》0011=0x3）；

4. RefIn：输入反转，为ture时反转，false时不反转, RefIn的反转：在计算输入反转时，反转的操作是针对每一个字节的bit位进行反转，字节数据之间仍保持原有顺序

5. Init：该参数的值有两种形式：全为0,全为1, 在判定并执行RefIn之后：当Init全为1时，表示在算法开始前对数据的前CRC位数（高位）先和对应位数个1进行异或（即：前CRC位数的值按位取反），再在后面补上CRC位数个0，才进行后续计算。当Init全为0时，在算法开始前对数据(这个数据是根据RefIn的值得到的)后面补上CRC位数个0后就可以进行后续计算；

6. RefOut-输出反转, RefIn和RefOut这两个值同时为ture或者同时为false. RefOut的反转：是将计算获得的CRC码整体进行反转，而不是像RefIn一样仅反转字节内的bit； 

7. XorOut：表示执行完RefOut之后，对结果进行异或全0或者全1（该例中为全0异或）。


# Code
	MODULE CRCModule
	    !*****************************************************
	    !Module Name:   CRCModule
	    !Version:       1.0
	    !Description:   
	    !Date:          2021-3-15
	    !Author:        Michael
	    !*****************************************************
	
	    RECORD RECORDCRCInfo
	        string NAME;
	        num WIDTH;
	        dnum POLY;
	        dnum INIT;
	        dnum XOROUT;
	        bool REFIN;
	        bool REFOUT;
	    ENDRECORD
	
	    CONST RECORDCRCInfo CRC_16_MODBUS:=["CRC-16/MODBUS",16,0X8005,0XFFFF,0X0000,TRUE,TRUE];
	
	    LOCAL PERS dnum CRCPoly:=0xa001;
	    LOCAL PERS dnum CRCResult:=0xfacb;
	    VAR errnum ERR_Width:=-1;
	
	    PROC TestCRC_16_MODBUS()
	        VAR byte data{4}:=[0X13,0X14,0X15,0X16];
	        VAR byte crc{2};
	        CRCResult:=GetCRC(data,CRC_16_MODBUS,crc);
	        Logging "CRCResult="+DecToHex(ValToStr(CRCResult));
	        Logging "CRC: "+DecToHex(ValToStr(crc{1}))+DecToHex(ValToStr(crc{2}));
	    ENDPROC
	
	    FUNC dnum GetCRC(byte data{*},RECORDCRCInfo crcInfo,INOUT byte crc{*})
	        BookErrNo ERR_Width;
	
	        IF crcInfo.REFIN THEN
	            IF crcInfo.WIDTH=8 THEN
	                CRCPoly:=NumToDnum(ReverseByte(DnumToNum(crcInfo.POLY)));
	            ELSEIF crcInfo.WIDTH=16 THEN
	                CRCPoly:=ReverseWord(crcInfo.POLY);
	            ELSE
	                !Only support CRC-8 or CRC-16
	                RAISE ERR_Width;
	            ENDIF
	        ELSE
	            CRCPoly:=crcInfo.POLY;
	        ENDIF
	
	        CRCResult:=crcInfo.INIT;
	        FOR i FROM 1 TO Dim(data,1) DO
	            CRCResult:=BitXOrDnum(CRCResult,NumToDnum(data{i}));
	            FOR j FROM 1 TO 8 DO
	                IF BitAndDnum(CRCResult,0X01)=NumToDnum(1) THEN
	                    CRCResult:=BitRShDnum(CRCResult,1);
	                    CRCResult:=BitXOrDnum(CRCResult,CRCPoly);
	                ELSE
	                    CRCResult:=BitRShDnum(CRCResult,1);
	                ENDIF
	            ENDFOR
	        ENDFOR
	
	        CRCResult:=BitXOrDnum(CRCResult,crcInfo.XOROUT);
	
	        IF crcInfo.REFOUT THEN
	            IF crcInfo.WIDTH=8 THEN
	                crc{1}:=DnumToNum(CRCResult);
	            ELSEIF crcInfo.WIDTH=16 THEN
	                crc{2}:=DnumToNum(BitAndDnum(CRCResult,0XFF));
	                crc{1}:=DnumToNum(BitAndDnum(BitRShDnum(CRCResult,8),0XFF));
	            ELSE
	                !Only support CRC-8 or CRC-16
	                RAISE ERR_Width;
	            ENDIF
	        ELSE
	            IF crcInfo.WIDTH=8 THEN
	                crc{1}:=DnumToNum(CRCResult);
	            ELSEIF crcInfo.WIDTH=16 THEN
	                crc{1}:=DnumToNum(BitAndDnum(CRCResult,0XFF));
	                crc{2}:=DnumToNum(BitAndDnum(BitRShDnum(CRCResult,8),0XFF));
	            ELSE
	                !Only support CRC-8 or CRC-16
	                RAISE ERR_Width;
	            ENDIF
	        ENDIF
	
	        RETURN CRCResult;
	
	    ERROR
	        IF ERRNO=ERR_Width THEN
	            Logging "Only support CRC-8 or CRC-16";
	            RAISE ;
	        ENDIF
	    ENDFUNC
	
	    FUNC byte ReverseByte(byte byteOri)
	        byteOri:=BitOr(BitAnd(BitRSh(byteOri,1),0b01010101),BitLSh(BitAnd(byteOri,0b01010101),1));
	        byteOri:=BitOr(BitAnd(BitRSh(byteOri,2),0x33),BitLSh(BitAnd(byteOri,0x33),2));
	        byteOri:=BitOr(BitAnd(BitRSh(byteOri,4),0x0F),BitLSh(BitAnd(byteOri,0x0F),4));
	        RETURN byteOri;
	    ENDFUNC
	
	    FUNC dnum ReverseWord(dnum dnumOri)
	        dnumOri:=BitOrDnum(BitAndDnum(BitRShDnum(dnumOri,1),0x5555),BitLShDnum(BitAndDnum(dnumOri,0x5555),1));
	        dnumOri:=BitOrDnum(BitAndDnum(BitRShDnum(dnumOri,2),0x3333),BitLShDnum(BitAndDnum(dnumOri,0x3333),2));
	        dnumOri:=BitOrDnum(BitAndDnum(BitRShDnum(dnumOri,4),0x0F0F),BitLShDnum(BitAndDnum(dnumOri,0x0F0F),4));
	        dnumOri:=BitOrDnum(BitAndDnum(BitRShDnum(dnumOri,8),0x00FF),BitLShDnum(BitAndDnum(dnumOri,0x00FF),8));
	        RETURN dnumOri;
	    ENDFUNC
	
	    FUNC dnum SwitchWordOrder(dnum dnumOri)
	        dnumOri:=BitOrDnum(BitAndDnum(BitRShDnum(dnumOri,8),0x00FF),BitAndDnum(BitLShDnum(dnumOri,8),0xFF00));
	        RETURN dnumOri;
	    ENDFUNC
	
	ENDMODULE